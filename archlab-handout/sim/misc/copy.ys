# copy_block - Copy src to dest and return xor checksum of src

# System execute
.pos 0
irmovq stack, %rsp
call main
halt

.align 8
# Source block
src:
	.quad 0x00a
	.quad 0x0b0
	.quad 0xc00

# Destination block
dest:
	.quad 0x111
	.quad 0x222
	.quad 0x333

main:
	irmovq src, %rdi
	irmovq dest, %rsi
	irmovq $3, %rdx
	call copy_block
	ret

# src in %rdi, dest in %rsi, len in %rdx
copy_block:
	pushq %rbx
	pushq %r12
	pushq %r13
	xorq %rax, %rax           # use %rax to store the result, and set it to 0
	irmovq $8, %r12
	irmovq $1, %r13

loop:
	andq %rdx, %rdx
	jle end

	mrmovq (%rdi), %rbx
	rmmovq %rbx, (%rsi)
	xorq %rbx, %rax
	addq %r12, %rdi
	addq %r12, %rsi
	subq %r13, %rdx
	jmp loop

end:
	popq %r13
	popq %r12
	popq %rbx
	ret

.pos 0x400
stack:

